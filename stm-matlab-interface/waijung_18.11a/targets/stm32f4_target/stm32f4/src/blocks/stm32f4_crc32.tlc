%implements stm32f4_crc32 "C"
%selectfile NULL_FILE

%function addParametersToBlock(block) void
   %assign sampletimestr = CAST("String",SFcnParamSettings.sampletimestr)
   %assign blockid = CAST("String",SFcnParamSettings.blockid)
   %assign block = block + sampletimestr + blockid
%endfunction

%function BlockTypeSetup(block, system) void
   %<addParametersToBlock(block)>
%endfunction

%function BlockInstanceSetup(block, system) void
   %<addParametersToBlock(block)>

   %openfile tmpBuf
      uint32_t %<blockid>_idx = 0;
   %closefile tmpBuf
   %assign file = LibCreateSourceFile("Source", "Custom","waijung_hwdrvlib")
   %<LibAddSourceFileCustomSection(file,"Declarations","cDefines")>
   %<LibSetSourceFileCustomSection(file,"cDefines",tmpBuf)>

   %openfile tmpBuf
      /* %<blockid> */
      void enable_%<blockid>(void){
         /* Enable CRC clock */
         RCC_AHB1PeriphClockCmd(RCC_AHB1Periph_CRC, ENABLE);
      }
   %closefile tmpBuf
   %assign file = LibCreateSourceFile("Source", "Custom","waijung_hwdrvlib")
   %<LibAddSourceFileCustomSection(file,"Declarations","cFunctions")>
   %<LibSetSourceFileCustomSection(file,"cFunctions",tmpBuf)>

   %openfile tmpBuf
      /* %<blockid> */
      void enable_%<blockid>(void);
   %closefile tmpBuf
   %assign file = LibCreateSourceFile("Header", "Custom", "waijung_hwdrvlib")
   %<LibAddSourceFileCustomSection(file,"Declarations","cFunctions")>
   %<LibSetSourceFileCustomSection(file,"cFunctions",tmpBuf)>
%endfunction

%function Outputs(block, system) Output
   %<addParametersToBlock(block)>
   /* %<blockid>: '%<block.Name>' */
   CRC_ResetDR();
   %assign rollVars = ["U"]
   %roll idx = RollRegions, lcv = 2, block, "Roller", rollVars
      CRC->DR = %<LibBlockInputSignal(0, "", lcv, idx)>;
   %endroll
   
   %assign y = LibBlockOutputSignal(0, "", "", 0)
   %<y> = CRC->DR;
%endfunction

%function Enable(block, system) Output
   %<addParametersToBlock(block)>
   enable_%<blockid>();
%endfunction

%function Disable(block, system) Output
   %<addParametersToBlock(block)>
   /* disable_%<blockid>(); */
%endfunction

%function Terminate(block, system) Output
   %<addParametersToBlock(block)>
   /* terminate_%<blockid>(); */
%endfunction

